gacpCheckDiff() {
    emulate -LR zsh

    local currentDir dir __old

    currentDir="$(pwd -P)"
    for dir in "${ZSH_GACP_BLACKLISTED_DIRECTORIES[@]}" ; do
        if [[ "$currentDir" == "$dir" ]]; then
            printf "\x1b[0;1;31m"
            zle -R "Zsh-Git-ACP BLACKLISTED: $(pwd -P)" && read -k 1
            printf "\x1b[0m"
            return 1
        fi
    done

    if ! zpwrIsGitDir; then
        printf "\x1b[0;1;31m"
        zle -R "Zsh-Git-ACP ERROR: $(pwd) is not a git dir." && read -k 1
        printf "\x1b[0m"
        return 1
    fi

    print -r -- "$BUFFER" | grep -q -E '\S' || {
        printf "\x1b[0;1;31m"
        zle -R "Zsh-Git-ACP ERROR: No commit message." && read -k 1
        printf "\x1b[0m"
        return 1
    }
    #leaky simonoff theme so reset ANSI escape sequences
	git add .

	git status | grep -q "nothing to commit" && {
        printf "\x1b[0;1;31m"
        zle -R "Zsh-Git-ACP ERROR: Nothing to commit." && read -k 1
        printf "\x1b[0m"
        return 1
	}

     __old="$LESS"
    unset LESS

    if zpwrExists gitSdiffColorizer.pl;then
        gitSdiffColorizer.pl | less -R
    else
        git difftool -y -x HEAD * | less -R
    fi

    export LESS="$__old"

    echo
    printf "\x1b[4;34m>>>>>> Push? \x1b[0m"
    if echo "$SHELL" | grep -q zsh ; then
        read -k 1
    else
        read -n 1
    fi

    if [[ "$REPLY" == 'y' ]]; then
        # blue foreground color git info
        printf "\x1b[0;34m"
        gacpCommitAndPush "$BUFFER" && {
            print -sr "$BUFFER"
            zle .kill-whole-line
            printf "\x1b[0m"
            zle .redisplay
        }
        printf "\x1b[0m"
    else
            print -sr "$BUFFER"
            echo
            zle .kill-whole-line
            zle .redisplay
    fi
}

gacpCheckDiff "$@"
